apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

apply plugin: "androidx.navigation.safeargs"
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

enum BuildType {
    DEBUG,
    RELEASE,
}

static def getAppName(BuildType buildType) {
    String APP_NAME = "Student"
    switch (buildType) {
        case BuildType.DEBUG:
            return APP_NAME + " - " + BuildType.DEBUG.name()
        case BuildType.RELEASE:
            return APP_NAME
    }
}

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.riteshakya.student"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        debug {
            def keystorePropertiesFile = file("keystore/debug.keystore.properties")
            def keystoreProperties = new Properties()
            keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

            storeFile file(keystoreProperties['store.file'])
            storePassword keystoreProperties['key.store.password']
            keyAlias keystoreProperties['key.alias']
            keyPassword keystoreProperties['key.alias.password']
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            resValue "string", "app_name", getAppName(BuildType.RELEASE)
        }
        debug {
            signingConfig signingConfigs.debug
            applicationIdSuffix ".debug"
            resValue "string", "app_name", getAppName(BuildType.DEBUG)
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    def dagger_version = '2.22.1'
    def navigation_version = '1.0.0'
    def glide_version = "4.8.0"

    implementation project(':core')
    implementation project(':ui')
    implementation project(':businesslogic')

    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    kapt "com.google.dagger:dagger-compiler:$dagger_version"
    kapt "com.google.dagger:dagger-android-processor:$dagger_version"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.0.2'
    api "android.arch.navigation:navigation-fragment-ktx:$navigation_version"
    api "android.arch.navigation:navigation-ui-ktx:$navigation_version"

    kapt "com.github.bumptech.glide:compiler:$glide_version"


    //image compressor
    api 'id.zelory:compressor:2.1.0'

    //video compressor
    api project(':videocompressor')

    //firestore ui
    implementation 'com.firebaseui:firebase-ui-firestore:4.3.1'

    //recycler view
    implementation 'com.android.support:recyclerview-v7:28.0.0'

    // Firebase crashlytics
    implementation 'com.google.firebase:firebase-core:16.0.9'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'
}

apply plugin: 'com.google.gms.google-services'
